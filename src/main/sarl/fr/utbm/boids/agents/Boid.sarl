package fr.utbm.boids.agents

import io.sarl.core.Initialize
import io.sarl.core.Logging
import io.sarl.core.DefaultContextInteractions
import fr.utbm.boids.events.DemandeDeplacement
import fr.utbm.boids.events.StartPosition
import fr.utbm.boids.events.ResultatDeplacement
import fr.utbm.boids.Vector
import java.util.Random
import fr.utbm.boids.events.ValidationDeplacement
import fr.utbm.boids.BoidBody
import fr.utbm.boids.environment.Obstacle
import java.util.concurrent.atomic.AtomicBoolean
import java.util.concurrent.atomic.AtomicInteger
import javafx.geometry.Point2D
import io.sarl.core.Lifecycle
import java.util.UUID
import fr.utbm.boids.gui.fx.EndSimulation
import java.util.Map
import fr.utbm.boids.events.BoidInitialized

agent Boid {
	uses Logging, DefaultContextInteractions, Lifecycle

	var body : BoidBody
	var parentAgent : UUID
	
	/*
	 * gestion des évènements
	 */
	 
	on Initialize {
		loggingName = "Boid-" + this.ID
		info("A Boid agent was started.")
		parentAgent = occurrence.spawner
		this.body = new BoidBody(occurrence.parameters.get(0) as Integer, 10, 2,occurrence.parameters.get(1) as Integer, occurrence.parameters.get(2) as Integer)
		info('MON BODY')
		info(this.body)
		emit(new BoidInitialized(this.body, 'Boid'))
	}
	
	on StartPosition {
		var validPosition : AtomicBoolean = new AtomicBoolean(true)
		var maPosition : Vector
		var rnd = new Random()
		var x : AtomicInteger = new AtomicInteger(0)
		var y : AtomicInteger = new AtomicInteger(0)
		var vx : AtomicInteger = new AtomicInteger(0)
		var vy : AtomicInteger = new AtomicInteger(0)	
		do {
			validPosition.set(true)
			x.set(rnd.nextInt(occurrence.width) + 1)
			y.set(rnd.nextInt(occurrence.height) + 1)
			occurrence.obstacles.forEach[o : Obstacle, index : int | 
				if(o.getPolygon().contains(new Point2D(x.get(), y.get()))) {
					validPosition.set(false)
				}
			]
		} while(validPosition.get() == false)
		
		info("X, Y \n" + x + " " + y)
		maPosition = new Vector(x.get(), y.get())
		
		//définition d'une vitesse aléatoire 
		vx.set(rnd.nextInt(this.body.groupeVitesseMax))
		vy.set(rnd.nextInt(this.body.groupeVitesseMax))
		this.body.newVitesse = new Vector (vx.get(),vy.get())
		
		// vmax est en ligne droite, on vérifie qu'avec vx et vy on dépasse pas la longueur de vmax
		if (this.body.newVitesse.length > this.body.groupeVitesseMax) {
			this.body.newVitesse.normaliser()
			this.body.newVitesse.fois(this.body.groupeVitesseMax)
		}
		
		emit(new ResultatDeplacement(maPosition, this.body.newVitesse))
	}

	// Si le déplacement est validé, on enregistre la nouvelle position du Boids, ainsi que sa nouvelle vitesse
	on ValidationDeplacement {
		this.body.position = occurrence.position
		this.body.vitesse = this.body.newVitesse
		info("Validation de la position")
		info("vitesse boids :    " + this.body.vitesse)
		info("position boids : " + this.body.position)
	}
	
	@SuppressWarnings("equals_with_null")
	on DemandeDeplacement {
		
		var boids = occurrence.boids
		var forceTot : Vector

		forceTot = new Vector(0, 0)
		 
		if (boids != null) {
			
			//on applique la séparation 
			forceTot.plus(separation(boids))
			
			//on applique la cohésion
			forceTot.plus(cohesion(boids))				
			
			//on applique l'alignement
			forceTot.plus(alignement(boids))

			//on applique la répulsion
			forceTot.plus(repulsion(boids))
			
			//on applique la force résultant de la détection des obstacles
		 
			// TODO appliquer force obstacle
		}
		
		//forceTot.fois(100000)
		info("force totale " + forceTot)

		emit(new ResultatDeplacement(appliquerForce(forceTot), this.body.newVitesse))
	 
	}
	
	/*
	 * fonction qui permet de déterminer si le boids en question est visible dans notre perception
	 */
	 
	 def visible (b : BoidBody) : boolean{
	 	
	 	var tmp : Vector
	 	var tmp2 : Vector
	 	
	 	
	 	tmp = new Vector(b.position)
	 	tmp.moins(this.body.position)

		//TODO insérer avec des paramètres la demi largeur et demi hauteur
		if (tmp.x > 400 ){
	 		tmp.moins(new Vector(800,0))
	 	}
	 	
	 	if (tmp.y > 300) {
	 		tmp.moins(new Vector(0,600))
	 	}
	 	
	 	
	 	// Si le boids est trop loin, on ne le voit pas 
	 	if (tmp.length() > this.body.distanceVisibilite){
	 		return false
	 	}
	 	
	 	tmp2 = new Vector(this.body.vitesse)
	 	
	 	// on test si le boids est hors de l'angle de vision : cos(ABC) = vec(AB).vec(AC)/(AB*AC)
	 	if (Math.abs(Math.toDegrees(Math.acos((tmp2.point(tmp)/(tmp2.length * tmp.length))))) > this.body.angleVisibilite) {
	 		return false
	 	}
	 	
	 	return true
	 }
	 
	
	/* 
	 * Création des fonctions comportementales du Boids
	 */
	 
	//TODO test si non utilisation du test du groupe à une influence
	@SuppressWarnings("equals_with_null")
	def separation(boids : Map<UUID, BoidBody>) : Vector {

		var force : Vector
		var tmp : Vector
		var len : double
		var xelem : double
		var yelem : double
		var tmpelem : Vector

		force = new Vector(0, 0)
		tmp = new Vector(0, 0)
		 
		for (elem : boids.entrySet) {
			if (elem.key != null && elem.value.groupe == this.body.groupe && visible(elem.value) && elem.key != this.ID) {
				
				//hauteur et largeur de la map mis en dur
				//TODO amener largeur et hauteur dynamiquement
				
				if(distance(this.body.position.x, elem.value.position.x) < distance(this.body.position.x, elem.value.position.x - 800)){
					if(distance(this.body.position.x, elem.value.position.x) < distance(this.body.position.x, elem.value.position.x + 800)){
						xelem = elem.value.position.x
					} else {
						xelem = elem.value.position.x + 800
					}
				} else {
					if(distance(this.body.position.x, elem.value.position.x - 800) < distance(this.body.position.x, elem.value.position.x + 800)){
						xelem = elem.value.position.x - 800
					} else {
						xelem = elem.value.position.x + 800
					}
				}
				
				if (distance(this.body.position.y, elem.value.position.y) < distance(this.body.position.y, elem.value.position.y - 600)) {
					if (distance(this.body.position.y, elem.value.position.y) < distance(this.body.position.y, elem.value.position.y + 600)) {
						yelem = elem.value.position.y
					} else {
						yelem = elem.value.position.y + 600
					}
				} else {
					if (distance(this.body.position.y, elem.value.position.y - 600) < distance(this.body.position.y, elem.value.position.y + 600)) {
						yelem = elem.value.position.y - 600
					} else {
						yelem = elem.value.position.y + 600
					}
				 }
					 
				
				tmpelem = new Vector(xelem%800, yelem%600)
				tmp.setXY(this.body.position)
				tmp.moins(tmpelem)
				len = tmp.length()
				tmp.fois(1 / (len * len))
				force.plus(tmp)
				force.fois(35)
			}
		}
	
		return force

	}

	@SuppressWarnings("equals_with_null")
	def cohesion(boids : Map<UUID, BoidBody>) : Vector {
		
		var nbTot = 0 
		var force : Vector 
		
		force = new Vector(0,0)
		 
		for (elem : boids.entrySet) {
			if (elem.key != null && elem.key != ID  && elem.value.groupe == this.body.groupe  && visible(elem.value)) {
				nbTot++
				force.plus(elem.value.position)
			}
		}
		
		if (nbTot > 0){
			force.fois(1/nbTot)
			force.moins(this.body.position)
		}
		force.fois(0.60)
		return force
	}
	
	

	@SuppressWarnings("equals_with_null")
	def alignement(boids : Map<UUID, BoidBody>) : Vector {
		
		var nbTot = 0 
		var force : Vector
		var tmp : Vector
		
		force = new Vector(0,0)
		tmp = new Vector(0,0)

		for (elem : boids.entrySet) {
			if (elem.key != null && elem.key != ID && elem.value.groupe == this.body.groupe && visible(elem.value)) {
				nbTot++
				tmp.setXY(elem.value.position)
				tmp.fois(1 / tmp.length)
				force.plus(tmp)
			}
		}
		
		if (nbTot > 0) {
			force.fois(1/nbTot)	
		}
		
		return force
		
	}

	@SuppressWarnings("equals_with_null")
	def repulsion(boids : Map<UUID, BoidBody>) : Vector {
		
		var force : Vector
		var tmp : Vector
		var len : double
		
		force = new Vector(0,0)
		tmp = new Vector(0,0)

		for (elem : boids.entrySet) {
			if (elem.key != null && elem.value.groupe != this.body.groupe && visible(elem.value)) {
				tmp.setXY(this.body.position)
				tmp.moins(elem.value.position)
				len = tmp.length
				tmp.fois(1 / (len * len))
				force.plus(tmp)
				force.fois(100)
			}
		}
		
		return force
	}
	
	
	// retourne la position du boid après l'application de la force, stoque en mémoire la nouvelle vitesse du Boids
	def appliquerForce(force : Vector) : Vector {

		// f = ma => a =f/m

		var newPosition : Vector		
		var acceleration : Vector
		
		acceleration = new Vector(force)
		//acceleration.fois(1/this.body.masse)
		info("acceleration : " + acceleration)
		
		this.body.newVitesse = new Vector(this.body.vitesse)
		this.body.newVitesse.plus(acceleration)
		
		//bornage de la vitesse
		if (this.body.newVitesse.length > this.body.groupeVitesseMax){
			this.body.newVitesse.normaliser()
			this.body.newVitesse.fois(this.body.groupeVitesseMax)
		}
		
		//maj position
		newPosition = new Vector(this.body.position)
		newPosition.plus(this.body.newVitesse)
		info(newPosition)
		return newPosition
	}
	
	def distance(XA : double, XB : double){
		return Math.abs(XA - XB)
	}
	
	on EndSimulation [isFrom(this.parentAgent)] {
		emit(new EndSimulation) [it.UUID == this.parentAgent]
		killMe
	}
	
}
